apply plugin: 'com.android.application'
import org.tmatesoft.svn.core.wc.*

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.fujifilm.sample.spa"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0-build_" + getSvnRevision()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == 'debug') {
            variant.outputs.each { output ->
                output.outputFile = new File(
                        output.outputFile.parent,
                        output.outputFile.name.replace("${project.name}-${variant.buildType.name}" + ".apk", "SampleApp_" + getSvnRevision() + ".apk"))
            }
        }
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:support-v4:23.1.1'
    compile 'net.yazeed44.imagepicker:imagepicker:1.3.0'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'ch.acra:acra:4.6.2'

    compile('com.github.afollestad.material-dialogs:core:0.8.5.1@aar') {
        transitive = true
    }

    compile('fr.avianey.com.viewpagerindicator:library:2.4.1@aar')


    compile project(':fujifilm.spa.sdk')
}

def getSvnRevision() {
    ISVNOptions options = SVNWCUtil.createDefaultOptions(true);
    SVNClientManager clientManager = SVNClientManager.newInstance(options);
    SVNStatusClient statusClient = clientManager.getStatusClient();
    SVNStatus status = statusClient.doStatus(projectDir, false);
    SVNRevision revision = status.getRevision();
    return revision.getNumber();
}
